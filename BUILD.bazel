load(
    "@bazel_skylib//rules:copy_file.bzl",
    "copy_file",
)

load(
    "@build_bazel_rules_nodejs//:index.bzl",
    "nodejs_binary",
)

load(
    "@rules_pkg//:pkg.bzl",
    "pkg_tar",
)

load(
    "//lib:defs.bzl",
    "version_purty",
)

alias(
    actual = select({
        ":linux": "linux-specific-purty",
        ":macos": "macos-specific-purty",
        ":windows": "windows-specific-purty",
    },
        no_match_error = "No purty binary available for this platform",
    ),
    name = "platform-specific-purty",
)

config_setting(
    name = "linux",
    constraint_values = [
        "@platforms//os:linux",
    ],
)

config_setting(
    name = "macos",
    constraint_values = [
        "@platforms//os:macos",
    ],
)

config_setting(
    name = "windows",
    constraint_values = [
        "@platforms//os:windows",
    ],
)

copy_file(
    is_executable = True,
    name = "linux-specific-purty",
    out = "bin/linux/purty",
    src = "//src:purty",
)

copy_file(
    is_executable = True,
    name = "macos-specific-purty",
    out = "bin/osx/purty",
    src = "//src:purty",
)

copy_file(
    is_executable = True,
    name = "windows-specific-purty",
    out = "bin/win/purty",
    src = "//src:purty",
)

exports_files([
    ".hlint.yaml",
], visibility = [
    "//:__subpackages__",
],
)

nodejs_binary(
    entry_point = "bin/purty.js",
    name = "purty.js",
)

pkg_tar(
    extension = "tar.gz",
    name = "purty-{version}-win".format(
        version = version_purty,
    ),
    srcs = [
        "//src:purty",
    ],
)

sh_test(
    args = [
        "--expected-version {version}".format(
            version = version_purty,
        ),
        "--purty $(location //src:purty)",
        "--verbose",
    ],
    data = glob([
        "test/acceptance/**/*",
    ]) + [
        "//src:purty",
    ],
    name = "test-acceptance-binary",
    srcs = [
        "test/acceptance.sh",
    ],
    tags = [
        "coverage_requires_jvm",
        "test_requires_bash",
    ],
)

sh_test(
    args = [
        "--expected-version {version}".format(
            version = version_purty,
        ),
        "--purty $(location :purty.js)",
        "--verbose",
    ],
    data = glob([
        "test/acceptance/**/*",
    ]) + [
        ":platform-specific-purty",
        ":purty.js",
    ],
    name = "test-acceptance-npm",
    srcs = [
        "test/acceptance.sh",
    ],
    tags = [
        "coverage_requires_jvm",
        "test_requires_bash",
    ],
)

test_suite(
    name = "lint",
    tags = [
        "lint",
    ],
    tests = [
        "//lib:lint",
        "//src:lint",
    ],
)
